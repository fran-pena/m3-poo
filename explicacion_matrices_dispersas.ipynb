{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "3d85d381",
   "metadata": {},
   "source": [
    "\n",
    "# Un ejemplo de programación orientada a objetos: matrices dispersas\n",
    "\n",
    "## Formatos de matrices dispersas\n",
    "\n",
    "Las matrices `sparse` de Matlab se guardan en formato _compressed sparse row_ (CSR) [[1]](#r1), mediante tres arreglos unidimensionales:\n",
    "- los valores no nulos, \n",
    "- las longitudes de las filas y \n",
    "- los índices de las columnas.\n",
    "\n",
    "Los valores se pueden indicar en formato _coordinate list_ (COO) [[2]](#r2),[[3]](#r3), es decir, indicando _(fila, columna, valor)_.\n",
    "\n",
    "El formato _Matrix Market_ [[4]](#r4) fue desarrollado (.mtx) por el National Institute of Standards and Technology (NIST) para facilitar el intercambio de matrices de prueba para computación numérica, especialmente en álgebra lineal dispersa. Es un formato basado en COO:\n",
    "1. La primera línea comienza con `%%MatrixMarket` seguido de varios campos :\n",
    "\n",
    "```text\n",
    "%%MatrixMarket matrix <format> <field> <symmetry>\n",
    "```\n",
    "donde:\n",
    "- `<format>` = coordinate (dispersa) o array (densa)\n",
    "- `<field>` = real, complex, integer o pattern\n",
    "- `<symmetry>` = general, symmetric, skew-symmetric, hermitian\n",
    "\n",
    "Nuestro ejemplo estará adaptado al caso `%%MatrixMarket matrix coordinate real symmetric`. En particular usaremos el fichero [bcsstk04.mtx](https://math.nist.gov/MatrixMarket/data/Harwell-Boeing/bcsstruc1/bcsstk04.html) indicado en un analisis estructural ejecutado por John Lewis (Boeing Computer Services) [[5]](#r5).\n",
    "\n",
    "Para uso intensivo de _Matrix Market_, se recomienda usar la libreria proporcionada en\n",
    "[https://math.nist.gov/MatrixMarket/mmio/f/mmio.f](https://math.nist.gov/MatrixMarket/mmio/f/mmio.f).\n",
    "\n",
    "2. La siguiente línea contiene el número de filas, columnas y (si es dispersa) número de entradas no nulas.\n",
    "\n",
    "3. Las siguientes filas representan una entrada en formato COO: fila columna valor\n",
    "\n",
    "Por ejemplo:\n",
    "\n",
    "```text\n",
    "%%MatrixMarket matrix coordinate real symmetric\n",
    "132 132 1890\n",
    "1 1  1.9960268182200e+03\n",
    "2 1  5.6751562001600e+02\n",
    "3 1  7.7541907594400e+02\n",
    "...\n",
    "```\n",
    "\n",
    "Para usar otros formatos de matriz, se recomienda revisar [[6]](#r6).\n",
    "\n",
    "**OBSERVACIÓN:**\n",
    "- Ni el formato COO ni la implementación de la clase matrix es la mejor para matrices dispersas, pero se usa por su interes pedagógico.\n",
    "- La idea a transmitir es que hay que usar las librerías especializadas.\n",
    "\n",
    "<a id=\"r1\">[1]</a> J. R. Gilbert, C. Moler, and R. Schreiber, “Sparse matrices in MATLAB: Design and implementation,” _SIAM Journal on Matrix Analysis and Applications_, vol. 13, no. 1, pp. 333–356, 1992, [doi: 10.1137/0613024](https://citeseerx.ist.psu.edu/document?repid=rep1&type=pdf&doi=79bb9997f4a9f80f5a7e34868fb458d553abf01c).\n",
    "\n",
    "<a id=\"r2\">[2]</a> MathWorks, \"Constructing sparse matrices,\" _MathWorks_, 2021. [https://es.mathworks.com/help/matlab/math/constructing-sparse-matrices.html](https://es.mathworks.com/help/matlab/math/constructing-sparse-matrices.html).\n",
    "\n",
    "<a id=\"r3\">[3]</a> Wikipedia, \"Sparse matrix: Coordinate list (COO),\" _Wikipedia_, 2025. [https://en.wikipedia.org/wiki/Sparse_matrix#Coordinate_list_(COO)](https://en.wikipedia.org/wiki/Sparse_matrix#Coordinate_list_(COO)).\n",
    "\n",
    "<a id=\"r4\">[4]</a> NIST, \"Matrix Market Exchange Formats\", National Institute of Standards and Technology, 2022. [https://math.nist.gov/MatrixMarket/formats.html#mtx](https://math.nist.gov/MatrixMarket/formats.html#mtx).\n",
    "\n",
    "<a id=\"r5\">[5]</a> NIST, \"BCSSTK04: Stiffness matrix from structural engineering problem\", Matrix Market, 2022. [https://math.nist.gov/MatrixMarket/data/Harwell-Boeing/bcsstruc1/bcsstk04.html](https://math.nist.gov/MatrixMarket/data/Harwell-Boeing/bcsstruc1/bcsstk04.html).\n",
    "\n",
    "<a id=\"r6\">[6]</a> J. Burkardt, _SPARSEKIT: Sparse matrix software in FORTRAN77_. Department of Scientific Computing, Florida State University. [https://people.sc.fsu.edu/~jburkardt/f77_src/sparsekit/sparsekit.html](https://people.sc.fsu.edu/~jburkardt/f77_src/sparsekit/sparsekit.html)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d45cafd9",
   "metadata": {},
   "source": [
    "### Clase `matrix`\n",
    "\n",
    "Crearemos un módulo (clase) para matrices dispersas en formato COO.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "11c8ea8e",
   "metadata": {},
   "source": [
    "\n",
    "Al ser un módulo largo, pegamos trozos en este Jupyter Notebook, lo que permite la coloración pero lo hace más rígido (no conocerá los cambios hechos en el código).\n",
    "\n",
    "```fortran\n",
    "module matrix_class\n",
    "use iso_Fortran_env, only: real64\n",
    "implicit none\n",
    "\n",
    "type :: matrix\n",
    "  private\n",
    "  integer :: n, m\n",
    "  integer, allocatable :: row(:), col(:)\n",
    "  real(real64), allocatable :: val(:)\n",
    "end type\n",
    "\n",
    "interface read;   module procedure read_matrix;  end interface\n",
    "interface size;   module procedure size_matrix;  end interface\n",
    "interface set;    module procedure set_sca;      end interface\n",
    "interface lu;     module procedure lu_matrix;    end interface\n",
    "interface get\n",
    "  module procedure get_sca\n",
    "  module procedure get_row\n",
    "  module procedure get_col\n",
    "end interface\n",
    "private :: read_matrix, size_matrix, get_sca, get_col, get_row, set_sca, lu_matrix\n",
    "\n",
    "contains\n",
    "```\n",
    "\n",
    "Se declara un tipo de dato `matrix` que guardará las variables (atributos de objeto):\n",
    "  - `n`, número de filas,\n",
    "  - `m`, número de columnas,\n",
    "  - `row`, array de índices de fila\n",
    "  - `row`, array de índices de columna\n",
    "  - `val`, array de valores no nulos\n",
    "\n",
    "Para aquellos procedimientos que vayan a tener otro nombre público, como `read_matrix` (bajo el nombre `read`):\n",
    "  - el nombre público se indica en su interfaz y\n",
    "  - se privatiza `matrix_read` para que no entre en conflicto con entidades declaradas en otros módulos."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6177be02",
   "metadata": {},
   "source": [
    "Empezamos los métodos con cómo desalojar un objeto de clase `matrix`: si sus atributos están alojados se desalojan.\n",
    "\n",
    "```fortran\n",
    "subroutine dealloc(this)\n",
    "type(matrix) :: this\n",
    "integer :: i, res\n",
    "character(255) :: cad\n",
    "\n",
    "if (allocated(this%row)) deallocate(this%row, stat = res, errmsg = cad)\n",
    "if (res /= 0) error stop '(matrix/dealloc) Unable to deallocate variable row: '//trim(cad)\n",
    "if (allocated(this%col)) deallocate(this%col, stat = res, errmsg = cad)\n",
    "if (res /= 0) error stop '(matrix/dealloc) Unable to deallocate variable col: '//trim(cad)\n",
    "if (allocated(this%val)) deallocate(this%val, stat = res, errmsg = cad)\n",
    "if (res /= 0) error stop '(matrix/dealloc) Unable to deallocate variable val: '//trim(cad)\n",
    "end subroutine\n",
    "```\n",
    "- Se advierte de cualquier problema surgido, mediante el estado `stat` y el mensaje de error `errmsg`. \n",
    "- Se toman cadenas de 260 porque es la longitud máxima de una ruta en Windows API [https://learn.microsoft.com/en-us/windows/win32/fileio/maximum-file-path-limitation](https://learn.microsoft.com/en-us/windows/win32/fileio/maximum-file-path-limitation).\n",
    "- `trim` sirve para no imprimir los blancos a la derecha."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a05743d9",
   "metadata": {},
   "source": [
    "Ahora, cómo alojar un objeto: \n",
    "```fortran\n",
    "subroutine alloc(this, row, col, val)\n",
    "type(matrix)             :: this\n",
    "integer,      intent(in) :: row(:), col(:)\n",
    "real(real64), intent(in) :: val(:)\n",
    "character(255) ::  cad\n",
    "\n",
    "if (size(row,1) /= size(col,1) .or. size(row,1) /= size(val,1)) &\n",
    "  error stop '(matrix/alloc) The three input arrays have different size.'\n",
    "if (size(row,1) <= 0) then\n",
    "  write(cad, *) size(row,1)\n",
    "  error stop '(matrix/alloc) The input arrays have an invalid size: '//trim(adjustl(cad))\n",
    "end if\n",
    "!alojamiento\n",
    "if (allocated(this%row) .or. allocated(this%col) .or. allocated(this%val)) call dealloc(this)\n",
    "allocate(this%row(size(row,1)), this%col(size(row,1)), this%val(size(row,1)))\n",
    "!valores iniciales\n",
    "this%row = row\n",
    "this%col = col\n",
    "this%val = val\n",
    "this%n = maxval(row)\n",
    "this%m = maxval(col)\n",
    "end subroutine\n",
    "```\n",
    "- Se le deben pasar tres arrays de igual tamaño.\n",
    "- Si alguno de los atributos está alojado, el objeto se desaloja primero.\n",
    "- Se asignan los atributos del objeto.\n",
    "- `error stop` comunica una salida por fallo (desde Fortran 2008). \n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "44361174",
   "metadata": {},
   "source": [
    "Seguidamente, escribimos la función para leer ficheros .mtx del tipo:\n",
    "  ```text\n",
    "  %%MatrixMarket matrix coordinate real symmetric\n",
    "  132 132 1890\n",
    "  1 1  1.9960268182200e+03\n",
    "  2 1  5.6751562001600e+02\n",
    "  3 1  7.7541907594400e+02\n",
    "  ...\n",
    "  ```\n",
    "```fortran\n",
    "subroutine read_matrix(this, filename)\n",
    "type(matrix)             :: this\n",
    "character(*), intent(in) :: filename\n",
    "character(255) :: cad\n",
    "integer :: iu, ios, nval, i\n",
    "\n",
    "open(newunit=iu, file = filename, iostat = ios, position='rewind')\n",
    "if (ios /= 0) error stop '(matrix/read_matrix) Unable to open '//trim(filename)\n",
    "read(iu,*) cad\n",
    "read(iu,*) this%n, this%m, nval\n",
    "print*, 'Read matrix ', trim(filename), ':', this%n, 'rows', this%m, 'cols', nval, 'values'\n",
    "if (allocated(this%row) .or. allocated(this%col) .or. allocated(this%val)) call dealloc(this)\n",
    "allocate(this%row(nval), this%col(nval), this%val(nval))\n",
    "do i = 1, nval\n",
    "  read(iu,*) this%row(i), this%col(i), this%val(i)\n",
    "end do\n",
    "close(iu)\n",
    "end subroutine\n",
    "```\n",
    "- Abre el fichero en una nueva unidad con `newunit`.\n",
    "- Advierte de un error mediante `iostat`.\n",
    "- Si el objeto está alojado, se desaloja previamente.\n",
    "- Para uso intensivo de _Matrix Market_, se recomienda usar la libreria proporcionada en [https://math.nist.gov/MatrixMarket/mmio/f/mmio.f](https://math.nist.gov/MatrixMarket/mmio/f/mmio.f).\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8434cd09",
   "metadata": {},
   "source": [
    "\n",
    "Ahora extendemos la función `size` a nuestra clase:\n",
    "\n",
    "```fortran\n",
    "function size_matrix(this, d) result(res)\n",
    "type(matrix), intent(in) :: this\n",
    "integer,      intent(in) :: d\n",
    "integer :: res\n",
    "character(255) :: cad\n",
    "\n",
    "select case (d)\n",
    "case(1)\n",
    "  res = this%n\n",
    "case(2)\n",
    "  res = this%m\n",
    "case default\n",
    "  write(cad, *) d\n",
    "  error stop '(matrix/size_matrix) The requested dimension is invalid: '//trim(adjustl(cad))\n",
    "end select\n",
    "end function\n",
    "```  \n",
    "- `size(objeto, 1)` y `size(objeto, 2)` devuelven el número de filas y columnas, respectivamente.\n",
    "- Cualquier otra dimensión devuelve error.\n",
    "- El texto del error mezcla cadenas y números; en Fortran debemos transformar el número mediente una _escritura interna_ en una cadena.\n",
    "- La cadena debe desplazarse a la izquierda con `adjustl` antes de aliminar los blancos de la derecha con `trim`."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2e06f5a9",
   "metadata": {},
   "source": [
    "Empezamos con las funciones que sirven para obtener valores de la matriz. Todas ellas están bajo la interfaz:\n",
    "  ```fortran\n",
    "  interface get \n",
    "    module procedure get_sca\n",
    "    module procedure get_row\n",
    "    module procedure get_col\n",
    "  end interface\n",
    "  ```\n",
    "La primera obtiene valores escalares:\n",
    " \n",
    "```fortran\n",
    "function get_sca(this, i, j) result(val)\n",
    "type(matrix), intent(in) :: this\n",
    "integer,      intent(in) :: i, j\n",
    "real(real64)             :: val\n",
    "integer :: k\n",
    "character(255) :: cad\n",
    "\n",
    "if (.not. allocated(this%row) .or. .not. allocated(this%col) .or. .not. allocated(this%val)) &\n",
    "  error stop '(matrix/get_sca) Matrix is not allocated.'\n",
    "if (size(this%row,1) < 0) then \n",
    "  write(cad, *) size(this%row, 1)\n",
    "  error stop '(matrix/get_sca) The matrix arrays have an invalid size: '//trim(adjustl(cad))\n",
    "end if\n",
    "do k = 1, size(this%row,1)\n",
    "  if (this%row(k) == i .and. this%col(k) == j) then\n",
    "    val = this%val(k)\n",
    "    return\n",
    "  end if\n",
    "end do\n",
    "val = 0._real64\n",
    "end function\n",
    "```\n",
    "- Si el objeto no está alojado, devuelve un error.\n",
    "- Si está alojado pero sin contenido, devuelve un error. Basta comprobar si `row` está alojado pues sólo `alloc` y `set` pueden crearlo.\n",
    "- Se busca la posición de _(i,j)_. Si no está, se devuelve cero.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "77e62711",
   "metadata": {},
   "source": [
    "Ahora la función que devuelve una sección columna:\n",
    "```fortran\n",
    "function get_col(this, i, j) result(val)\n",
    "type(matrix), intent(in)  :: this\n",
    "integer,      intent(in)  :: i(:), j\n",
    "real(real64)              :: val(size(i,1))\n",
    "integer :: k\n",
    "\n",
    "if (size(i,1) < 0) error stop '(matrix/get_col) Insufficient number of indices.'\n",
    "do k = 1, size(i,1)\n",
    "  val(k) = get(this, i(k), j)\n",
    "end do\n",
    "end function\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "31b463d0",
   "metadata": {
    "vscode": {
     "languageId": "shellscript"
    }
   },
   "source": [
    "Ahora la que devuelve una sección fila:\n",
    "```fortran\n",
    "function get_row(this, i, j) result(val)\n",
    "type(matrix), intent(in)  :: this\n",
    "integer,      intent(in)  :: i, j(:)\n",
    "real(real64)              :: val(size(j,1))\n",
    "integer :: k\n",
    "\n",
    "if (size(j,1) < 0) error stop '(matrix/get_row) Insufficient number of indices.'\n",
    "do k = 1, size(j,1)\n",
    "  val(k) = get(this, i, j(k))\n",
    "end do\n",
    "end function\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3c020e18",
   "metadata": {},
   "source": [
    "Ahora, la función para establecer valores. Se indica el caso escalar y que acoge a la interfaz `get`, que podría ser ampliada en el futuro con funciones que establezcan secciones de la matriz.\n",
    "\n",
    "```fortran\n",
    "subroutine set_sca(this, i, j, val)\n",
    "type(matrix), intent(inout) :: this\n",
    "integer,      intent(in)    :: i, j\n",
    "real(real64), intent(in)    :: val\n",
    "integer ::  k, s\n",
    "integer, allocatable :: trow(:), tcol(:)\n",
    "real(real64), allocatable :: tval(:)\n",
    "\n",
    "if (.not. allocated(this%row) .or. .not. allocated(this%col) .or. .not. allocated(this%val)) then\n",
    "  call alloc(this, [i], [j], [val])\n",
    "else\n",
    "  s = size(this%row,1)\n",
    "  do k = 1, s\n",
    "    if (this%row(k) == i .and. this%col(k) == j) then\n",
    "      this%val(k) = val\n",
    "      return\n",
    "    end if\n",
    "  end do\n",
    "  allocate(trow(s+1), tcol(s+1), tval(s+1))\n",
    "  trow(1:s) =  this%row; trow(s+1) = i\n",
    "  tcol(1:s) =  this%col; tcol(s+1) = j\n",
    "  tval(1:s) =  this%val; tval(s+1) = val\n",
    "  call move_alloc(trow, this%row)\n",
    "  call move_alloc(tcol, this%col)\n",
    "  call move_alloc(tval, this%val)\n",
    "  this%n = max(this%n, i)\n",
    "  this%m = max(this%m, j)\n",
    "end if\n",
    "end subroutine\n",
    "```    \n",
    "- Si no está alojada, se crea una matriz $i\\times j$ y se inserta el (unico) valor.\n",
    "- Si está alojada, \n",
    "  - Si ya existe la posición $(i,j)$, se colocal el nuevo valor.\n",
    "  - Si no existe, se crean tres arrays temporales, `trow`, `tcol` y `tval`, con una posición más y luego el objeto apunta a los nuevos arrayscon `move_alloc`. Esto reduce el trabajo de _crear, copiar, destruir_.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1cae49c8",
   "metadata": {},
   "source": [
    "Veamos cómo queda la factorización LU, echando mano de los métodos `get` y `set`. Recordamos la fórmula para calcular $U_{ij}$ en matrices llenas:\n",
    "   ```fortran\n",
    "   u(i,j) = a(i,j) - dot_product(l(i,1:i-1), u(1:i-1,j))\n",
    "   ```\n",
    "   que ahora se transforma en:\n",
    "   ```fortran\n",
    "   call set(u, i, j, get(a, i, j) - dot_product(get(l, i, [(k, k=1,i-1)]), get(u, [(k, k=1,i-1)], j)))\n",
    "   ```\n",
    "\n",
    "```fortran\n",
    "subroutine lu_matrix (a, l, u)\n",
    "type(matrix), intent(in) :: a\n",
    "type(matrix), intent(out) :: l, u\n",
    "integer :: i, j, k\n",
    "\n",
    "do j = 1, size(a,2)\n",
    "  do i = 1, j\n",
    "    call set(u, i, j, get(a, i, j) - dot_product(get(l, i, [(k, k=1,i-1)]), get(u, [(k, k=1,i-1)], j)))\n",
    "  end do\n",
    "  call set(l, j, j, 1._real64)\n",
    "  do i = j+1, size(a,2)\n",
    "    call set(l, i, j, (get(a, i, j) - dot_product(get(l, i, [(k, k=1,j-1)]), get(u, [(k, k=1,j-1)], j))) / get(u, j, j))\n",
    "  end do\n",
    "end do\n",
    "end subroutine\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "47dd833a",
   "metadata": {},
   "source": [
    "Ya podemos usar la clase para trabajar con una matrix _Matrix Market_."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d12ce181",
   "metadata": {},
   "outputs": [],
   "source": [
    "!pygmentize -g m3-poo/src/main.f90"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9e16102d",
   "metadata": {},
   "source": [
    "## Ejercicios\n",
    "1. Usa las funciones para devolver una sección fila o columna. Como el resultado es un array de tamaño variable, se recomienda usar la cláusula `source` introducida en Fortran 2003:\n",
    "  ```fortran\n",
    "  real(real64), allocatable :: b(:)`\n",
    "  type(matrix) :: a\n",
    "  ...\n",
    "  allocate(b, source=get(a, 1, [(i, i=1,size(a,2))]))\n",
    "  ```\n",
    "Equivaldría en Matlab a `b = a(1,:)`.\n",
    "\n",
    "2. Implementa una función para establecer una sección fila o columna.\n",
    "\n",
    "3. Implementa la multiplicación matriz por vector y extiende `matmul` con ella."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9c059b6b",
   "metadata": {},
   "source": [
    "## Más ejercicios a proponer\n",
    "1. Implementar remonte y descenso para el tipo `matrix`.\n",
    "2. Mostrar el tiempo de cálculo de la factorización en función de la dimensión; ver en el curso de Cocalc, la carpeta *fortran/met_num_mat/ANM/lu/llena/tiempo/*. Usar el paquete ogpf."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f1526944",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "PowerShell",
   "language": "powershell",
   "name": "powershell"
  },
  "language_info": {
   "codemirror_mode": "shell",
   "file_extension": ".ps1",
   "mimetype": "text/x-sh",
   "name": "powershell"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
